#NEXUS

BEGIN PSODA;

  begin pow(base = 0, exponent = 1);
    $base = base;
    $exponent = exponent;
    if ($exponent == 0)
      return 1;
    endif;

    $result = $base;
    $i = 0;
    while ($i < ($exponent - 1))
      $result = $result * $base;
      $i++;
    endwhile;
    return $result;
  end;

  begin doubleDiv(numerator = 0, denominator = 1, precision = 4);
    $numerator = numerator;
    $denominator = denominator;
    $precision = precision;

    $precisionFactor = pow(base = 10, exponent = $precision);
    $extendedNumerator = $numerator * $precisionFactor;
    $extendedResult = $extendedNumerator / $denominator;
    $textResult = "";
    $i = 0;
    
    $decimalDown = 0;
    $resultLeft = $extendedResult;
    $precisionLeft = $precisionFactor;
    while ($i < $precision) 
      $thisResultNum = $resultLeft / $precisionLeft;

      [ Check for round on the last digit ]
      if (($i + 1) >= $precision)
        if ((($resultLeft % $precisionLeft) * 2) >= $precisionLeft)
          $thisResultNum++;
	endif;
      endif;

      $textResult = $textResult . $thisResultNum;
      $resultLeft = $resultLeft % $precisionLeft;
      $precisionLeft = $precisionLeft / 10;

      [ Check for decimal point ]
      if (($decimalDown == 0) && ($resultLeft / $precisionFactor) < 1)
        $decimalDown = 1;
        $textResult = $textResult . ".";
      endif;
      $i++;
    endwhile;
    return $textResult;
  end;

  begin printDoubleDiv(numerator = 0, denominator = 1, precision = 4);
    $numerator = numerator;
    $denominator = denominator;
    $precision = precision;

    $result = doubleDiv(numerator = $numerator, denominator = $denominator, precision = $precision);
    print ( $numerator . " / " . $denominator . " = " . $result . "\n");
  end;

  begin printQuotients(numIterations = 10);
    $i = 0;
    $numIterations = numIterations;
    while ($i < $numIterations)
      printDoubleDiv(numerator = $i);
      $i++;
    endwhile;
  end;

  printQuotients;
  print ( "\n");

  set precision = 6;
  set denominator = 35;
  printQuotients(numIterations = 5);
  print ( "\n");

  set precision = 3;
  printQuotients(numIterations = 10, myParam = 10);
  print ( "\n");

  set denominator = 4;
  printQuotients(numIterations = 10);
  print ( "\n");

  set precision = 2;
  printQuotients(numIterations = 10);
  print ( "\n");

END;
